apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

version = project.idPdfViewerVersion
group = project.idGroup

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.3'
    ndkVersion '23.0.7123448 rc1'

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 1
        versionName project.idPdfViewerVersion
        consumerProguardFiles 'consumer-rules.pro'
    }

    tasks.withType(Javadoc).all {
        enabled false
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set('sources')
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                groupId project.idGroup
                artifactId project.idArtifactPdfViewer

                name project.idArtifactPdfViewer
                description project.idDescriptionPdfViewer
                url project.idUrl

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id project.idDeveloperID
                        name project.idDeveloperName
                        email project.idDeveloperEmail
                    }
                }

                scm {
                    connection project.idVcsUrl
                    developerConnection project.idVcsUrl
                    url project.idUrl
                }
            }
        }
    }
}

bintray {
    user = project.idUser
    key = project.idPass

    pkg {
        repo = project.idRepo
        name = project.idArtifactPdfViewer
        configurations = ['archives']
        desc = project.idDescriptionPdfViewer
        websiteUrl = project.idUrl
        vcsUrl = project.idVcsUrl
        licenses = ['Apache-2.0']
        publish = true
        publicDownloadNumbers = true
    }
}

dependencies {
    implementation project(path: ':AhmerPdfium')
    implementation 'com.github.ahmerafzal1:AhmerUtils:4.2.5'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'org.jetbrains:annotations:20.1.0'
}